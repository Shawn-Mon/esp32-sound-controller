#include <WiFi.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C lcd(0x27, 16, 2);

// ===== WIFI SETTINGS =====
const char* ssid = "ESP32_NET";
const char* password = "12345678";

const char* serverIP = "192.168.137.1"; // Laptop hotspot IP
const int serverPort = 5050;
// ========================

WiFiClient client;

#define JOY_Y 35
#define JOY_BTN 32

unsigned long lastMove = 0;

void setup() {
  Serial.begin(115200);
  pinMode(JOY_BTN, INPUT_PULLUP);

  lcd.init();
  lcd.backlight();
  lcd.print("Connecting WiFi");

  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    lcd.print(".");
  }

  lcd.clear();
  lcd.print("WiFi Connected");

  while (!client.connect(serverIP, serverPort)) {
    lcd.setCursor(0, 1);
    lcd.print("Connecting PC");
    delay(1000);
  }

  lcd.clear();
  lcd.print("Connected!");
}

void loop() {
  int y = analogRead(JOY_Y);
  int btn = digitalRead(JOY_BTN);

  if (millis() - lastMove > 300) {
    if (y < 1000) {
      client.println("UP");
      lastMove = millis();
    }
    else if (y > 3000) {
      client.println("DOWN");
      lastMove = millis();
    }
  }

  if (btn == LOW) {
    client.println("PLAY");
    delay(300);
  }

  if (client.available()) {
    String msg = client.readStringUntil('\n');
    if (msg.startsWith("SHOW:")) {
      lcd.clear();
      lcd.print(msg.substring(5));
    }
  }
}
